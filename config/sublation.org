#+TITLE: Sublation
#+PROPERTY: header-args :noweb yes

#  LocalWords:  CLI Atuin VCS FZF Zoxide Gluetun Torrenting Vaultwarden Copyparty

* Shells

** Base environment

#+begin_src scheme :noweb-ref home-services
  (simple-service 'common-environment-variables
      home-environment-variables-service-type
    '(("PAGER" . "less -RKF")))
#+end_src

** Bash

#+begin_src scheme :noweb-ref home-modules
  (gnu home services shells)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-bash-service-type
    (home-bash-configuration
      (bashrc
       (list
        ;; 2025-12-06: My default shell is bash because I haven't gotten
        ;; Tramp to work with Fish shell yet.  (I've deduced that the
        ;; prompt is not the problem.)  So, instead, I keep bash as my
        ;; shell and dispatch to fish if the current process wasn't
        ;; started by tramp
        (plain-file "to-fish.bash"
          "if [[ $- == *i* ]] && { [[ ! $TERM =~ dumb ]] || [[ $INSIDE_EMACS == *,eat* ]]; }; then
      SHELL=$(command -v fish) exec fish
  fi")))))
#+end_src

** Fish

#+begin_src scheme :noweb-ref home-modules
  (gnu packages shells)
  (gnu home services shells)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-fish-service-type
    (home-fish-configuration
      (config
       (list (plain-file "fish_greeting.fish" "set -g fish_greeting")))
      (environment-variables
       `(("SHELL" . ,(file-append fish "/bin/fish"))))
      (abbreviations '(("cd" . "z")))))
#+end_src

* Command line

** Common CLI tools

System monitoring packages:
#+begin_src scheme :noweb-ref home-packages
  "btop"
#+end_src
See also [[#h:20251222T054114.716045][Goaccess: Web log access analytics]].

Build systems and programming languages:
#+begin_src scheme :noweb-ref home-packages
  "git"
  "make"
  "cmake"
  "python"
#+end_src

File-system searching and visualization:
#+begin_src scheme :noweb-ref home-packages
  "tree"
  "ripgrep"
  "fd"
  "jq"
#+end_src

Partition management:
#+begin_src scheme :noweb-ref home-packages
  "parted"
#+end_src

** Replacements for essential CLI utilities

*** Bat: Fancier ~cat~

Configurable. Options include output with syntax highlighting, line numbers, and automatic piping to ~less~.

#+begin_src scheme :noweb-ref home-modules
  (abbe packages rust)
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "bat"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-fish-bat
      home-fish-service-type
    (home-fish-extension
      (aliases
       `(("cat" . ,(string-join '("bat" "--theme=ansi"
                                  "--style=plain,header-filesize,grid,snip --paging auto"
                                  "--italic-text=always --nonprintable-notation=caret")))))))
  (simple-service 'pager-environment-variables
      home-environment-variables-service-type
    ;; Use bat as a pager for man.  Taken from
    ;; https://github.com/sharkdp/bat?tab=readme-ov-file#man
    '(("MANPAGER" . "sh -c 'sed -u -e \"s/\\x1B\\[[0-9;]*m//g; s/.\\x08//g\" | bat -p -lman'")))
#+end_src

*** Zoxide: A DWIM ~cd~

#+begin_src scheme :noweb-ref home-modules
  (krisb services shells)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-zoxide-service-type
    (home-zoxide-configuration
      (zoxide (@ (abbe packages rust) zoxide))))
#+end_src

*** Procs: Fancier ~ps~

#+begin_src scheme :noweb-ref home-packages
  "procs"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-fish-procs
      home-fish-service-type
    (home-fish-extension
      (abbreviations `(("ps" . "procs")))))
#+end_src

** Control machine

#+begin_src scheme :noweb-ref home-packages
  "brightnessctl"
#+end_src

** Jujutsu: Distributed VCS

#+begin_src scheme :noweb-ref home-modules
  (abbe packages rust)
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "jujutsu"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'krisb-symlink-jj-config-files-service-type
        home-xdg-configuration-files-service-type
      `(("jj/config.toml"
         ,(local-file "files/jujutsu/config.toml"))))
#+end_src

Compatibility with the fish shell:
#+begin_src scheme :noweb-ref home-services
  (simple-service 'fish-vcs-jj
        home-xdg-configuration-files-service-type
      `(("fish/functions/fish_jj_prompt.fish"
         ,(local-file "files/jujutsu/fish_jj_prompt.fish"))
        ("fish/functions/fish_vcs_prompt.fish"
         ,(local-file "files/jujutsu/fish_vcs_prompt.fish"))))
#+end_src

** FZF: Generic fuzzy matching interface

#+begin_src scheme :noweb-ref home-modules
  (gnu packages terminals)                ; fzf
  (gnu packages rust-apps)                ; fd
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "fzf"
  "fd"
#+end_src

Bespoke integration with Fish. The initial inspiration for this was [[https://github.com/gazorby/fifc][gazorby/fifc]].
#+begin_src scheme :noweb-ref home-services
  (simple-service 'fish-fzf-packages
      home-profile-service-type
    (list fd fzf))                        ; Function dependencies
  (simple-service 'fish-fzf-function
        home-xdg-configuration-files-service-type
      `(("fish/functions/fzf_completion.fish"
         ,(local-file "files/fish/fzf_complete.fish"))))
  (simple-service 'fish-fzf-config
      home-fish-service-type
    (home-fish-extension
      (config
       (list (plain-file "fzf_custom.fish" "bind \\t fzf_complete")))))
#+end_src

** Atuin: Share shell history across shells and machines

# TODO 2026-01-04: Consider self-hosting a sync server. See
# https://docs.atuin.sh/cli/self-hosting/server-setup/

#+begin_src scheme :noweb-ref home-modules
  (krisb services shells)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-atuin-service-type
    (home-atuin-configuration
      (atuin-fish-flags '("--disable-up-arrow"))
      (atuin-bash-flags '("--disable-up-arrow"))))
  (simple-service 'krisb-symlink-atuin-config-files-service-type
      home-xdg-configuration-files-service-type
    `(("atuin/config.toml"
       ,(local-file "files/atuin/config.toml"))))
#+end_src

* Authentication

** GnuPG

#+begin_src scheme :noweb-ref home-modules
  (gnu packages gnupg)
  (gnu home services gnupg)
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "gnupg"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-gpg-agent-service-type)
#+end_src

** SOPS-Guix: Secrets management in Guix

#+begin_src scheme :noweb-ref home-modules
  (sops secrets)
  (sops home services sops)
#+end_src

#+begin_src scheme :noweb-ref home-helpers
  (define sops-sublation-secrets-file
    (string-append (dirname (current-filename)) "/files/sops/sublation.yaml"))

  ;; Get secrets as strings.  Taken from
  ;; https://github.com/fishinthecalculator/sops-guix/issues/2
  (use-modules ((ice-9 popen) #:select (open-input-pipe close-pipe))
               ((rnrs io ports) #:select (get-string-all))
               ((sops secrets) #:select (sanitize-sops-key)))

  (define* (get-sops-secret key #:key file (number? #f))
    (let* ((cmd (format #f "sops --decrypt --extract '~a' '~a'"
                        (sanitize-sops-key key)
                        file))
           (port (open-input-pipe cmd))
           (secret (get-string-all port)))
      (close-pipe port)
      (if number?
          (string->number secret)
          secret)))

  ;; Helper for getting file path of secret
  (define (get-sops-secret-path filename)
    (string-append "/run/user/" (number->string (getuid))
                   "/secrets/" filename))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "age"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-sops-secrets-service-type
    (home-sops-service-configuration
      (config (local-file "files/sops/sops.yaml" "sops.yaml"))
      (secrets
       (list
        <<home-sops-secrets>>))))
#+end_src

* Podman: Containers

#+begin_src scheme :noweb-ref system-modules
  (gnu services containers)
  (gnu services sysctl)
#+end_src

#+begin_src scheme :noweb-ref supplementary-groups
  "cgroup"
#+end_src

#+begin_src scheme :noweb-ref system-services
  ;; Rootless podman also needs 'iptables-service-type' specifically for
  ;; its (non-internal) networks; 'nftables-service-type' will not
  ;; suffice.  See (guix) Miscellaneous Services.  We may have both,
  ;; though, since the default ruleset for iptables is to accept
  ;; everything.
  (service iptables-service-type)
  (service rootless-podman-service-type
    (rootless-podman-configuration
      (subgids (list (subid-range (name "krisbalintona"))))
      (subuids (list (subid-range (name "krisbalintona"))))))
  ;; TODO 2025-12-22: Figure out an ergonomic solution to avoid this.
  ;; For rootless podman services (Caddy and Pihole)
  (simple-service 'sysctl-podman
      sysctl-service-type
    '(("net.ipv4.ip_unprivileged_port_start" . "53")))
#+end_src

Use Podman by default for ~home-oci-service-type~.
#+begin_src scheme :noweb-ref home-services
  (service home-oci-service-type
    (for-home
     (oci-configuration
      (runtime 'podman)                   ; Use podman instead of docker
      (verbose? #t))))
#+end_src

* OpenSSH

#+begin_src scheme :noweb-ref system-modules
  (gnu services ssh)
#+end_src

#+begin_src scheme :noweb-ref system-services
  (service openssh-service-type)
#+end_src

#+begin_src scheme :noweb-ref home-modules
  (gnu home services ssh)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-openssh-service-type
      (home-openssh-configuration
        (hosts
         (list
          (openssh-host (name "WindowsG14")
                        (host-name "192.168.4.138")
                        (user "krisbalintona")
                        (forward-x11? #t)
                        (forward-x11-trusted? #t))))))
#+end_src

#+begin_src scheme :noweb-ref system-fail2ban-jails
  (simple-service 'fail2ban-openssh
      fail2ban-service-type
    (list
     (fail2ban-jail-configuration
       (name "sshd")
       (enabled? #t)
       (max-retry 5)
       (find-time "10m")
       (ban-time "1h"))))
#+end_src

* Self-hosting

#+begin_src scheme :noweb-ref home-helpers
  (define services-dir
    (string-append (getenv "HOME") "/services"))
#+end_src

** Security

*** Principles and guidelines
:PROPERTIES:
:CUSTOM_ID: h:20251227T085128.494786
:END:

Guiding rules:
1. Set up a firewall.
2. Use a reverse proxy.
3. Each service gets their own container.
4. Use rootless containers.
5. Instead of =--network host=, each container has its own separate container network, preferably an internal one.
6. If publishing ports, don't publish to all interfaces, only to specific interfaces. That is, do =127.0.0.1:80:80= instead of =80:80=. We prefer the former to the latter since the latter permits listening to the ports from outside the host. (This is especially dangerous on some distros that bypass the firewall when publishing ports on e.g. Docker: port 80 ends up visible to the outside.)
7. Geo-block/fence. Several ways to do so[fn:1]:
   * Firewall (e.g., nftables)
   * [[https://github.com/friendly-bits/geoip-shell][geoip-shell]]
   * Reverse-proxy-level
     + [[https://github.com/fabriziosalmi/caddy-waf][caddy-waf]]
     + [[https://github.com/porech/caddy-maxmind-geolocation][caddy-maxmind-geolocation]]
   * Web application firewall (WAF)
     + [[https://github.com/corazawaf/coraza-geoip?tab=readme-ov-file][coraza-geoip]]
8. Have a web application firewall (WAF) to deny web-specific trickery
   * [[https://github.com/corazawaf/coraza][coraza]] (Caddy integration: [[https://github.com/corazawaf/coraza-caddy][coraza-caddy]])
9. When possible, prefer Unix Domain Sockets (UDS) (on shared container volumes) over publishing ports. Avoids having to have a network and published port(s) for containers.
10. If possible, use a non-root user for containers (even rootless ones).

Helpful tips for self-hosting:
+ [[https://www.reddit.com/r/selfhosted/comments/1ag9q88/comment/koftjss/?context=3][This Reddit comment]][fn:2]

Informative articles on security principles:
+ [[https://www.cloudflare.com/learning/security/glossary/what-is-zero-trust/][Zero Trust security | What is a Zero Trust network?]]

[fn:1] To test if geo-blocking is working, use a tool like https://check-host.net/check-http

[fn:2] See also [[https://www.reddit.com/r/selfhosted/comments/1cv2l3q/security_psa_for_anyone_using_docker_on_a/][this relevant Reddit post]].
   
*** Nftables: Firewall

I minimize the number of ports I have open and I route all internet traffic through my reverse proxy. (See also [[#h:20251227T085128.494786][Principles and guidelines]].)

#+begin_src scheme :noweb-ref system-modules
  (gnu packages linux)
  (gnu services networking)
  (ice-9 textual-ports)                   ; For 'get-string-all'
  (krisb packages networking)
#+end_src

#+begin_src scheme :noweb-ref system-services
  (service nftables-service-type
    (nftables-configuration
      (ruleset
       (local-file "files/nftables/sublation.nft"))))
#+end_src

*** Fail2ban: Scan logs and ban malicious actors 

Doing something like ~sudo fail2ban-client status --all~ shows the ban activity for all fail2ban jails.

#+begin_src scheme :noweb-ref system-modules
  (gnu services security)
#+end_src

Several of my services integrate with ~fail2ban-service-type~.
#+begin_src scheme :noweb-ref system-services
  (service fail2ban-service-type)
  <<system-fail2ban-jails>>
#+end_src

*** CrowdSec: Security based on client behavioral patterns

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-crowdsec
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        ;; Should be an external network because CrowdSec requires
        ;; host-outbound internet requests
        (name "crowdsec-network"))))
     (containers
      (list
       (oci-container-configuration
         (provision "crowdsec")
         (image "crowdsecurity/crowdsec:latest")
         (environment
          (list '("TZ" . "America/Chicago")
                '("LOCAL_API_URL" . "http://127.0.0.1:7200")
                (string-append "'"
                               "COLLECTIONS="
                               (string-join '("crowdsecurity/linux"
                                              "crowdsecurity/sshd"
                                              "crowdsecurity/whitelist-good-actors"
                                              "crowdsecurity/base-http-scenarios"
                                              "crowdsecurity/caddy")
                                            " ")
                               "'")
                ;; Enable Write-Ahead Logging with SQLite.  Disable if
                ;; using a filesystem over the network, e.g., NAS
                '("USE_WAL" . "true")
                ;; Bouncers
                (cons "BOUNCER_KEY_caddy"
                      (get-sops-secret '("caddy" "crowdsec-bouncer" "api-key")
                                       #:file sops-sublation-secrets-file))))
         (network "crowdsec-network")
         (ports '("127.0.0.1:7200:7200"))
         (volumes
          (list (cons "/home/krisbalintona/services/crowdsec/data/" "/var/lib/crowdsec/data/")
                (cons "/home/krisbalintona/services/crowdsec/config/" "/etc/crowdsec")
                (cons (string-append (dirname (current-filename)) "/files/crowdsec/config.yaml")
                      "/etc/crowdsec/config.yaml")
                (cons (string-append (dirname (current-filename)) "/files/crowdsec/acquis.yaml")
                      "/etc/crowdsec/acquis.yaml")
                ;; All Caddy logs
                (cons "/home/krisbalintona/services/caddy/log" "/var/log/caddy")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-crowdsec
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-crowdsec"
      #:schedule "0 0 */2 * *"
      #:files (list (string-append services-dir "/crowdsec")))))
#+end_src

*** Pocket ID: An out-of-the-box OIDC-compliant IdP provider

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-helpers
  (define pocket-id-socket-dir
      (string-append (getenv "XDG_RUNTIME_DIR")
                     "/pocket-id"))

  (define pocket-id-socket
    (string-append pocket-id-socket-dir "/pocket-id.sock"))
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("pocket-id-encryption-key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-pocket-id-socket
      home-shepherd-service-type
    (list
     (shepherd-service
       (provision '(home-oci-pocket-id-socket))
       (one-shot? #t)
       (start
        #~(lambda ()
            (mkdir-p #$pocket-id-socket-dir)
            (format #t "Socket directory exists at: ~a~%" #$pocket-id-socket-dir)
            #t))
       (documentation "Create parent directory for Copyparty socket."))))
  (simple-service 'home-oci-pocket-id
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "pocket-id-network")
        (internal? #t))))
     (containers
      (list
       (let ((encryption-key-file
              (get-sops-secret-path "pocket-id-encryption-key")))
         (oci-container-configuration
           (provision "pocket-id")
           (requirement '(home-oci-pocket-id-socket))
           ;; TODO 2026-01-02: Use the hardened distroless images they
           ;; have.  Though requires more setup since the Pocket ID
           ;; process runs as non-root and their default distro
           ;; containers configure persmissiona automatically; to make
           ;; permissions work in a fresh container (which Guix does on
           ;; shepherd service restart) I have to set up those
           ;; permissions myself.  See https://pocket-id.org/d
           ;; ocs/advanced/hardening
           (image "ghcr.io/pocket-id/pocket-id:v2")
           (environment
            (list "PORT=3111"
                  "TRUST_PROXY=true"    ; Whether behind a reverse proxy
                  "APP_URL=https://pocket-id.kristofferbalintona.me"
                  (cons "ENCRYPTION_KEY_FILE" encryption-key-file)
                  "PUID=1000"             ; Default
                  "PGID=1000"             ; Default
                  ;; Use unix sockets (UDS)
                  (cons "UNIX_SOCKET" pocket-id-socket)
                  ;; When false (default), send a "heartbeat" to add my
                  ;; instance to the total Pocket ID count.  Although
                  ;; I'd like to keep this false, this container's
                  ;; network is internal, so it cannot actually send the
                  ;; heartbeat.  This results in a bunch of extraneous
                  ;; messages in the log.  Maybe in the future I can
                  ;; figure out how to get around this while maintaining
                  ;; network security...
                  "ANALYTICS_DISABLED=true"))
           (network "pocket-id-network")
           (volumes
            `(("/home/krisbalintona/services/pocket-id/data" . "/app/data")
              ,(cons encryption-key-file encryption-key-file)
              ,(cons pocket-id-socket-dir pocket-id-socket-dir)))
           (extra-arguments '("--userns=keep-id"))
           (auto-start? #t)
           (respawn? #f)))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-pocket-id
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-pocket-id"
      #:schedule "0 0 */4 * *"
      #:files (list (string-append services-dir "/pocket-id/data")))))
#+end_src

** Networking

Some useful tooling:
#+begin_src scheme :noweb-ref home-packages
  "bind:utils"
#+end_src

*** Network-related packages

#+begin_src scheme :noweb-ref home-packages
  "curl"
  "wget"
  ;; Port scanning
  "nmap"
  "masscan"
#+end_src

*** DNS

**** Unbound: Privacy-centered DNS resolver
:PROPERTIES:
:CUSTOM_ID: h:20260105T091837.031550
:END:

I use Pihole as a DNS forwarder (see [[#h:20260105T091835.257202][Pi-hole: DNS filter]]) with Unbound being my DNS resolver.

#+begin_src scheme :noweb-ref system-modules
  (gnu services dns)
#+end_src

#+begin_src scheme :noweb-ref system-services
  ;; I use Unbound with Pihole.  For an explanation of why (and a guide
  ;; on how to do so) I pair Unbound with the latter, see
  ;; https://docs.pi-hole.net/guides/dns/unbound/
  (service unbound-service-type
    (unbound-configuration
      (server
       (unbound-server
         ;; Let only these interfaces query Unbound (Pihole forwards DNS
         ;; queries to Unbound)
         (interface '("127.0.0.1"         ; IPv4 local host
                      "::1"               ; IPv6 local
                      ;; Pihole sets "host.containers.internal" as the
                      ;; upstream DNS, so allow queries from this
                      ;; device's interface.  (See also the
                      ;; "access-control" setting.)
                      "192.168.4.242"))
         (tls-cert-bundle "/etc/ssl/certs/ca-certificates.crt")
         (hide-version #t)
         (hide-identity #t)
         ;; See
         ;; https://unbound.docs.nlnetlabs.nl/en/latest/manpages/unbound.conf.html
         ;; for a description of all options
         (extra-options
          '((port . 5335) ; Pihole forwards DNS queries to this port
            ;; These are already default, but I declare them explicitly
            ;; anyway
            (do-ip4 . "yes")
            (do-udp . "yes")
            (do-tcp . "yes")
            ;; The following can be set to "yes" if my WAN supports
            ;; IPv6.  I can check by going to test-ipv6.com
            (do-ip6 . "no")
            ;; All paths below are relative to CHROOT, if set.  Make
            ;; sure CHROOT has the permissions of the unbound process.
            ;; The default user of the process is "unbound" (see also
            ;; the USERNAME option).  To change permissions, do
            ;; something like:
            ;;
            ;;     sudo chown unbound:unbound CHROOT_PATH
            ;;
            ;; I do not CHROOT since unbound already runs as a user
            ;; without elevated privileges, and that's good enough for
            ;; me.
            (chroot . "")
            ;; Logging.  If LOGFILE is a path, then ensure its parent
            ;; directory is created and that LOGFILE or its directory is
            ;; owned by "unbound" (the default value of the USERNAME
            ;; option).  If LOGFILE is not set or is set to an empty
            ;; string, then log output to "sudo herd status unbound"
            ;; instead
            (verbosity . "1")  ; Default
            (logfile . "")
            (log-time-ascii . "yes")
            (log-destaddr . "yes")
            (log-queries . "yes")
            (log-servfail . "yes")
            ;; TODO 2025-12-16: Write a service to do this
            ;; automatically.  But first check if there truly is no
            ;; currently existing way to do it automatically.
            ;;
            ;; Use DNSSEC.
            ;;
            ;; NOTE 2025-12-12: On Guix Systems, the root key has to be
            ;; created manually, it seems.  Ensure the parent directory
            ;; of the file exists then run:
            ;;
            ;;     sudo unbound-anchor -a /PATH/TO/KEY/root.key
            ;;
            ;; Unbound also runs as the "unbound" user, so for extra
            ;; security you can change the permissions of the file, too:
            ;;
            ;;     sudo chown unbound:unbound /PATH/TO/KEY
            ;;
            ;; Or, the first command can be ran with "-u unbound", like
            ;; so:
            ;;
            ;;     sudo -u unbound unbound-anchor -a /PATH/TO/KEY/root.key
            ;;
            (auto-trust-anchor-file . "/var/lib/unbound/root.key")
            (harden-glue . "yes")
            (harden-dnssec-stripped . "yes")
            ;; Give minimal domain name information in queries to DNS
            ;; servers.  See also the 'qname-minimisation-strict'
            ;; setting.
            (qname-minimisation . "yes")
            ;; See
            ;; https://docs.pi-hole.net/guides/dns/unbound/#configure-unbound
            ;; for an explanation of these values
            (edns-buffer-size . "1232")
            (so-rcvbuf . "1m")
            (use-caps-for-id . "no")
            (num-threads . "1")
            (prefetch . "yes")))))
      (remote-control
       (unbound-remote
         (control-enable #t)
         ;; Use with:
         ;;
         ;;     sudo unbound-control -s CONTROL-INTERFACE status
         ;;
         (control-interface "/run/unbound.sock"))) ; Default value
      ;; We place the below in EXTRA-CONTENT because we either need to
      ;; unquote a value entirely in the config or quote portions of
      ;; them (which can be done if the cdr is a single Guile symbol).
      ;; But UNBOUND-SERVER always quotes values, so to deal with edge
      ;; cases we place certain settings in EXTRA-CONTENT.  (There can
      ;; be multiple "server:" blocks in the config, it seems.)
      (extra-content
       "server:
          access-control: 127.0.0.0/8 allow
          # Pihole sets \"host.containers.internal\" as the upstream #
          # DNS, so allow queries from this device's IP.  (See also the
          # \"interface\" setting.)
          access-control: 192.168.4.0/22 allow

          # Ensure privacy of local IP ranges.  Taken from
          # https://docs.pi-hole.net/guides/dns/unbound/#configure-unbound
          private-address: 192.168.0.0/16
          private-address: 169.254.0.0/16
          private-address: 172.16.0.0/12
          private-address: 10.0.0.0/8
          private-address: fd00::/8
          private-address: fe80::/10

          # Ensure no reverse queries to non-public IP ranges (RFC6303
          # 4.2).  Taken from
          # https://docs.pi-hole.net/guides/dns/unbound/#configure-unbound
          private-address: 192.0.2.0/24
          private-address: 198.51.100.0/24
          private-address: 203.0.113.0/24
          private-address: 255.255.255.255/32
          private-address: 2001:db8::/32")))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "unbound"
#+end_src

**** Pi-hole: DNS filter
:PROPERTIES:
:CUSTOM_ID: h:20260105T091835.257202
:END:

I use Pihole as a DNS forwarder, setting the only upstream DNS server to be my self-hosted Unbound instance (see [[#h:20260105T091837.031550][Unbound: Privacy-centered DNS resolver]]). Effectively, Pihole filters DNS requests, and forwards the ones that need to be resolved to Unbound (when the request isn't already cached by Pihole itself).

Regarding monitoring: aside from the webserver UI, uers can also do something like ~podman exec -it <name of pihole container> padd~ for an ncurses (i.e., CLI) UI that shows an abbreviated version of the statistics shown in the webserver UI. And logs can be viewed in the CLI via ~podman exec -it <name of pihole container> pihole tail~ (or just regular ~tail -F~ on any log file).

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("pihole-webserver-password"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-pihole
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (let* ((password-file
               (get-sops-secret-path "pihole-webserver-password")))
         (oci-container-configuration
           (provision "pihole")
           (image "docker.io/pihole/pihole:latest")
           (environment
            `("TZ=America/Chicago"
              "FTLCONF_dns_port=53" ; Default port
              ;; Webserver
              "FTLCONF_webserver_port=127.0.0.1:7080"
              ,(cons "WEBPASSWORD_FILE" password-file)
              ;; Listen for queries on all interfaces and from all
              ;; origins
              "FTLCONF_dns_listeningMode=ALL"
              ;; Forward queries to Unbound DNS (whose port is 5335 on
              ;; the host)
              "FTLCONF_dns_upstreams=127.0.0.1#5335"
              ;; Pihole as NTP server for other devices?
              "FTLCONF_ntp_ipv4_active=false"
              "FTLCONF_ntp_ipv6_active=false"
              ;; Pihole as NTP server for host?
              "FTLCONF_ntp_sync_active=false"))
           ;; We use the host network since clients need to directly
           ;; talk to pihole otherwise pihole can't distinguish clients
           ;; (an internal container network would make all clients come
           ;; from the same client)
           (network "host")
           (volumes
            (list (cons "/home/krisbalintona/services/pihole/data" "/etc/pihole")
                  (cons password-file password-file)))
           (auto-start? #t)
           (respawn? #f)))))))
#+end_src

# TODO 2026-01-03: I haven't found a way to have Pihole's files be owned (or at least readable by)
# the host user
# #+begin_src scheme :noweb-ref home-restic-backup-jobs
#   (simple-service 'home-restic-pihole
#       home-restic-backup-service-type
#     (list
#      (restic-job/defaults
#       #:name "restic-pihole"
#       #:schedule "0 0 */2 * *"
#       #:files (list (string-append services-dir "/pihole")))))
# #+end_src

*** Web traffic

**** Caddy: Reverse proxy

#+begin_src scheme :noweb-ref home-modules
  (krisb packages networking)
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("caddy" "netlify-access-token"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
  (sops-secret
    (key '("caddy" "pocket-id" "client-id"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
  (sops-secret
    (key '("caddy" "pocket-id" "client-secret"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
  (sops-secret
    (key '("caddy" "crowdsec-bouncer" "api-key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "caddy-security-netlify-crowdsec-coraza-maxmind"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-caddy
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (let ((netlify-access-token
              (get-sops-secret-path "caddy/netlify-access-token"))
             (pocket-id-client-id
              (get-sops-secret-path "caddy/pocket-id/client-id"))
             (pocket-id-client-secret
              (get-sops-secret-path "caddy/pocket-id/client-secret"))
             (crowdsec-bouncer-api-key
              (get-sops-secret-path "caddy/crowdsec-bouncer/api-key")))
         (oci-container-configuration
           (provision "caddy")
           (requirement '(home-oci-copyparty-socket home-oci-pocket-id-socket))
           (image
             (oci-image
               ;; OCI images locations follow a
               ;; [registry]/[repository]:[tag] format.  Since we are
               ;; local, we only need to specify a repository and
               ;; optionally a tag.  The REPOSITORY field below
               ;; corresponds to the [repository] of an OCI image
               ;; location; it can be whatever we want since this image is
               ;; created locally (in the Guix store)
               (repository "caddy-security-netlify-crowdsec-coraza-maxmind")
               (tag "2.10.2")
               (value (specifications->manifest '("coreutils"
                                                  "caddy-security-netlify-crowdsec-coraza-maxmind")))
               (pack-options '(#:symlinks (("/bin" -> "bin")
                                           ;; MaxMind database files
                                           ("/var/lib/geoip" -> "/var/lib/geoip"))))))
           ;; These environment variables are set in the Docker image
           ;; Caddy distributes (shown by e.g. "podman image inspect
           ;; docker.io/caddy:2.10.2").  My tests show that they need to
           ;; be set for some reason
           (environment
            `("CADDY_VERSION=v2.10.2"
              "XDG_CONFIG_HOME=/config"
              "XDG_DATA_HOME=/data"))
           ;; Use the host network, then for services expose to the host
           ;; only the required ports and have Caddy direct traffic to
           ;; those ports.  An added benefit to using the host network is
           ;; that it permits Caddy to log the real IP of clients, since
           ;; the NAT of the Podman bridge network is no longer an
           ;; intermediary
           (network "host")
           (volumes
            (list (cons "/home/krisbalintona/services/caddy/data" "/data") ; Path of XDG_DATA_HOME
                  (cons "/home/krisbalintona/services/caddy/log" "/data/log")
                  (cons (string-append (dirname (current-filename)) "/files/caddy/Caddyfile")
                        "/config/Caddyfile")
                  ;; Goaccess real-time web page
                  (cons "goaccess_web" "/var/www/goaccess")
                  ;; Unix sockets
                  (cons copyparty-socket-dir copyparty-socket-dir)
                  (cons pocket-id-socket-dir pocket-id-socket-dir)
                  ;; Secrets.  Reference these in the Caddyfile with
                  ;; file placeholders; see
                  ;; https://caddyserver.com/docs/conventions#placeholders
                  (cons netlify-access-token netlify-access-token)
                  (cons pocket-id-client-id pocket-id-client-id)
                  (cons pocket-id-client-secret pocket-id-client-secret)
                  (cons crowdsec-bouncer-api-key crowdsec-bouncer-api-key)))
           (command '("caddy" "run" "--config" "/config/Caddyfile"))
           (auto-start? #t)
           (respawn? #f)))))))
#+end_src

#+begin_src scheme :noweb-ref system-fail2ban-jails
  (simple-service 'fail2ban-caddy
      fail2ban-service-type
    (list 
     (fail2ban-jail-configuration
       (name "caddy-bots")
       (enabled? #t)
       (max-retry 3)
       (find-time "5m")
       (ban-time "1h")
       (log-path
        '("/home/krisbalintona/services/caddy/log/copyparty.log"))
       (filter
        (fail2ban-jail-filter-configuration
          (name "nginx-botsearch"))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-caddy
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-caddy"
      #:schedule "0 0 */2 * *"
      #:files (list (string-append services-dir "/caddy")))))
#+end_src

**** Wireguard: VPN

#+begin_src scheme :noweb-ref system-modules
  (gnu services vpn)
  (gnu services sysctl)
#+end_src

#+begin_src scheme :noweb-ref system-services
  (service wireguard-service-type
    (wireguard-configuration
      (shepherd-requirement '(nftables))
      (addresses '("10.0.0.1/24"))
      (port "53020")
      ;; TODO 2025-12-21: Avoid hardcoding this
      (private-key "/run/user/1000/secrets/wireguard-private-key")
      (bootstrap-private-key? #f)
      ;; Network rules
      (pre-up
       (list
        ;; Create IPv4 and IPv6 table for address translation (rewriting
        ;; packet addresses)
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " add table inet wg-nat")
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " add chain inet wg-nat postrouting '{ type nat hook postrouting priority -100; }'")
        ;; Allow Wireguard traffic to occur as if from the host (this
        ;; peer).  Do this by rewriting source IP of Wireguard outgoing
        ;; packets (packets from peers having a source IP from
        ;; 10.0.0.0/24, that is, from within my Wireguard subnet (which
        ;; are IPs from 10.0.0.0 to 10.0.0.255)) leaving via wifi
        ;; (interface wlp108s0) to have a source IP of the host's IP on
        ;; the wifi network (wlp108s0).
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " add rule inet wg-nat postrouting oifname 'wlp108s0'"
                         " ip saddr 10.0.0.0/24 masquerade")))
      (post-down
       (list
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " delete table inet wg-nat")))
      (peers
       (list
        (wireguard-peer
          (name "OnePlus")
          (public-key "Mgj/EOTOJv96q6NSN8CC7DEXB7ic6WV78H1ukHm7fyY=")
          (allowed-ips '("10.0.0.2/32")))))))
  ;; For Wireguard IP forwarding
  (simple-service 'sysctl-wireguard
      sysctl-service-type
    '(("net.ipv4.ip_forward" . "1")))
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("wireguard-private-key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

** Services

*** Copyparty: Fast and simple file server

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services shepherd)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-helpers
  (define copyparty-socket-dir
    (string-append (getenv "XDG_RUNTIME_DIR")
                   "/copyparty"))

  (define copyparty-socket
    (string-append copyparty-socket-dir "/copyparty.sock"))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-copyparty-socket
      home-shepherd-service-type
    (list
     (shepherd-service
       (provision '(home-oci-copyparty-socket))
       (one-shot? #t)
       (start
        #~(lambda ()
            (mkdir-p #$copyparty-socket-dir)
            (format #t "Socket directory exists at: ~a~%" #$copyparty-socket-dir)
            #t))
       (documentation "Create parent directory for Copyparty socket."))))
  (simple-service 'home-oci-copyparty
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "copyparty")
         (requirement '(home-oci-copyparty-socket))
         (image "docker.io/copyparty/ac:1.19.21")
         ;; Have files mounted at /data and copyparty config + cache
         ;; files in /srv
         (volumes
          `(("/home/krisbalintona/services/copyparty/data" . "/data")
            ("/home/krisbalintona/services/copyparty/log" . "/var/log/copyparty")
            (,(string-append (dirname (current-filename)) "/files/copyparty/copyparty.conf")
             . "/srv/copyparty.conf")
            ,(cons copyparty-socket-dir copyparty-socket-dir)))
         (command '("-c" "/srv/copyparty.conf"
                    "--chdir" "/srv"
                    ;; Logging
                    "-lo" "/var/log/copyparty/copyparty-%Y-%m%d-%H%M%S.txt"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-copyparty
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-copyparty"
      #:schedule "0 12 * * *"
      #:files (list (string-append services-dir "/copyparty")))))
#+end_src

*** Vaultwarden: Self-hosted password manager

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("vaultwarden" "push-installation-id"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
  (sops-secret
    (key '("vaultwarden" "push-installation-key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-vaultwarden
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "vaultwarden-network"))))
     (containers
      (list
       (oci-container-configuration
         (provision "vaultwarden")
         (image "vaultwarden/server:latest")
         (environment
          `("DOMAIN=https://vault.home.kristofferbalintona.me"
            ,(cons "ADMIN_TOKEN"
                   "$argon2id$v=19$m=19456,t=2,p=1$gbzi7DRoZ+SnGVSkAuZ482w7fkXTHrdRcHUJMG24CfI$vs2Xu3ikIopqOJYf319nGEtyz08NBuXE4I9gWVRjUew")
            ;; TODO 2025-12-25: Add logrotate configuration?
            ;; Logging
            "LOG_FILE=/var/log/vaultwarden/vaultwarden.log"
            "LOG_LEVEL=debug"
            "EXTENDED_LOGGING=true"
            ;; Push notification support.  Also requires (i) the
            ;; "firebaseinstallations.googleapis.com" domain not to be
            ;; blocked and (ii) be able to make outbound HTTPS
            ;; connections (e.g., requires an "external" container
            ;; network).  See
            ;; https://github.com/dani-garcia/vaultwarden/wiki/Enabling-Mobile-Client-push-notification
            "PUSH_ENABLED=true"
            ,(cons "PUSH_INSTALLATION_ID"
                   (get-sops-secret '("vaultwarden" "push-installation-id")
                                    #:file sops-sublation-secrets-file))
            ,(cons "PUSH_INSTALLATION_KEY"
                   (get-sops-secret '("vaultwarden" "push-installation-key")
                                    #:file sops-sublation-secrets-file))
            ;; Settings relevant for security.  See also
            ;; https://github.com/dani-garcia/vaultwarden/wiki/Hardening-Guide
            "SHOW_PASSWORD_HINT=false"
            "SIGNUPS_ALLOWED=false"))
         (network "vaultwarden-network")
         (ports '("127.0.0.1:7000:80"))
         (volumes
          '(("/home/krisbalintona/services/vaultwarden/data" . "/data")
            ("/home/krisbalintona/services/vaultwarden/log" . "/var/log/vaultwarden")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-vaultwarden
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-vaultwarden"
      #:schedule "0 0 * * *"
      #:files (list (string-append services-dir "/vaultwarden")))))
#+end_src

*** Torrenting

**** Gluetun: Bind all torrent client traffic to a VPN

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("gluetun" "wireguard_private_key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
  (sops-secret
    (key '("gluetun" "wireguard_preshared_key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-gluetun
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "gluetun-network")
        (subnet "10.89.6.0/24"))))
     (containers
      (list
       (let ((wireguard_private_key
              (get-sops-secret '("gluetun" "wireguard_private_key")
                               #:file sops-sublation-secrets-file))
             (wireguard_preshared_key
              (get-sops-secret '("gluetun" "wireguard_preshared_key")
                               #:file sops-sublation-secrets-file)))
         (oci-container-configuration
           (provision "gluetun")
           ;; See
           ;; https://github.com/qdm12/gluetun-wiki/blob/main/setup/readme.md#setup
           ;; for instructions on setting up Gluetun
           (image "qmcgaw/gluetun:latest")
           (host-environment
            (list
             (cons "WIREGUARD_PRIVATE_KEY" wireguard_private_key)
             (cons "WIREGUARD_PRESHARED_KEY" wireguard_preshared_key)))
           (environment
            '("TZ='America/Chicago'"
              ;; See
              ;; https://github.com/qdm12/gluetun-wiki/blob/main/setup/providers/windscribe.md
              ;; for Windscribe-specific Gluetun instructions
              "VPN_SERVICE_PROVIDER=windscribe"
              
              ;; Wireguard configuration
              ;;
              ;; Output from generated Wireguard config from
              ;; https://windscribe.com/myaccount#configgenerator-wireguard.
              ;; See all Wireguard options here:
              ;; https://github.com/qdm12/gluetun-wiki/blob/main/setup/options/wireguard.md
              ;;
              ;; Interface
              "VPN_TYPE=wireguard"
              "WIREGUARD_PRIVATE_KEY"
              "WIREGUARD_ADDRESSES='100.82.80.101/32'"
              ;; Peer (VPN server)
              "WIREGUARD_PUBLIC_KEY=6O1bKP+apj/JT/aV++aODc1+EHlPO+c0xGyfKXE+k14=" ; Optional
              "WIREGUARD_ENDPOINT_PORT=65142"
              "WIREGUARD_PRESHARED_KEY"
              
              ;; See
              ;; https://github.com/qdm12/gluetun-wiki/blob/main/setup/servers.md
              ;; for a list of VPN provider server regions and cities.
              ;; Choose values corresponding to the location chosen in the
              ;; config generated by Windscribe (see above)
              "SERVER_REGIONS='US East'"
              "SERVER_CITIES=Chicago"
              "UPDATER_PERIOD=24h"    ; Automatically update server list
              
              ;; Container firewall rules
              ;;
              ;; General firewall information and default behavior:
              ;; https://github.com/qdm12/gluetun-wiki/blob/main/faq/firewall.md.
              ;; See
              ;; https://github.com/qdm12/gluetun-wiki/blob/main/setup/options/firewall.md
              ;; for all firewall options
              "FIREWALL_INPUT_PORTS=9091"   ; Transmission web UI
              "FIREWALL_INPUT_PORTS=6701")) ; qBittorrent web UI
           (network "gluetun-network")
           (ports '("127.0.0.1:9091:9091"   ; Transmission web UI
                    "127.0.0.1:6701:6701")) ; qBittorrent web UI
           (extra-arguments '("--device=/dev/net/tun:/dev/net/tun"
                              "--cap-add=NET_ADMIN"
                              "--cap-add=NET_RAW")) ; For UDP health checks
           (volumes
            `(("/home/krisbalintona/services/gluetun/data" . "/gluetun")))
           (auto-start? #t)
           (respawn? #f)))))))
#+end_src

**** Transmission: Lightweight torrent client

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-transmission
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "transmission")
         (requirement '(gluetun))
         ;; See https://github.com/linuxserver/docker-transmission for
         ;; instructions on setting up Gluetun
         (image "lscr.io/linuxserver/transmission:latest")
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"))
         ;; NOTE: The transmission web UI is exposed by the Gluetun
         ;; container
         (network "container:gluetun")
         (volumes
          '(("/home/krisbalintona/services/transmission/config" . "/config")
            ("/home/krisbalintona/services/transmission/watch" . "/watch")
            ("/home/krisbalintona/services/media/downloads/bittorrent" . "/downloads")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

**** qBittorrent: Feature-full torrent client

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-qbittorrent
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "qbittorrent")
         (requirement '(gluetun))
         (image "linuxserver/qbittorrent:latest")
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"
            "TORRENTING_PORT=6299" ; 2026-01-09: Setting this doesn't have an effect in my setup
            "WEBUI_PORT=6701"))
         (network "container:gluetun")
         (volumes
          '(("/home/krisbalintona/services/qbittorrent/config" . "/config")
            ("/home/krisbalintona/services/qbittorrent/log" . "/log")
            ("/home/krisbalintona/services/media" . "/data")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

To "disable" the authentication page in the qBittorrent web UI, add the Gluetun network subnet address to the whitelisted IP subnets list in the "Web UI" tab in the settings. The subnet can be seen in the qBittorrent logs (the =qbittorrent.log= file in the log directory): search for lines with " WebAPI login success. IP: ...".

**** Sonarr: Get shows

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-sonarr
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "sonarr")
         (image "linuxserver/sonarr:latest")
         ;; See all environment variables here:
         ;; https://wiki.servarr.com/sonarr/environment-variables
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"
            "SONARR__SERVER__PORT=15151"))
         (network "gluetun-network")
         (ports '("127.0.0.1:15151:15151"))
         (volumes
          '(("/home/krisbalintona/services/sonarr/data" . "/config")
            ("/home/krisbalintona/services/sonarr/log" . "/config/logs")
            ("/home/krisbalintona/services/media" . "/data")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

**** Radarr: Get movies

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-radarr
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "radarr")
         (image "linuxserver/radarr:latest")
         ;; See all environment variables here:
         ;; https://wiki.servarr.com/radarr/environment-variables
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"
            "RADARR__SERVER__PORT=14100"))
         (network "gluetun-network")
         (ports '("127.0.0.1:14100:14100"))
         (volumes
          '(("/home/krisbalintona/services/radarr/data" . "/config")
            ("/home/krisbalintona/services/radarr/log" . "/config/logs")
            ("/home/krisbalintona/services/media" . "/data")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

*** Prowlarr: Centralize trackers

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-prowlarr
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "prowlarr")
         (image "lscr.io/linuxserver/prowlarr:latest")
         ;; See all environment variables here:
         ;; https://wiki.servarr.com/prowlarr/environment-variables
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"
            "PROWLARR__SERVER__PORT=13031"))
         (network "gluetun-network")
         (ports '("127.0.0.1:13031:13031"))
         (volumes
          '(("/home/krisbalintona/services/prowlarr/data" . "/config")
            ("/home/krisbalintona/services/prowlarr/log" . "/config/logs")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

*** Jellyfin: Media streamer

Jellyfin can integrate with Pocket ID via the [[https://github.com/9p4/jellyfin-plugin-sso][Jellyfin SSO plugin]]. For instructions on how to do this, see https://pocket-id.org/docs/client-examples/jellyfin.

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-jellyfin
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "jellyfin-network"))))
     (containers
      (list
       (oci-container-configuration
         (provision "jellyfin")
         ;; NOTE 2026-01-10: I've implemented the hardware acceleration
         ;; instructions specific to my device as instructed by this
         ;; maintainer; if I change the image in the future, I must
         ;; change my configuration elsewhere to support hardware
         ;; acceleration.  See those instructions here:
         ;; https://github.com/linuxserver/docker-jellyfin?tab=readme-ov-file#hardware-acceleration-enhancements
         (image "linuxserver/jellyfin:latest")
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"
            "JELLYFIN_PublishedServerUrl=https://jellyfin.kristofferbalintona.me"
            ;; For hardware acceleration support, enable the linuxserver
            ;; OpenCL-Intel mod; see
            ;; https://github.com/linuxserver/docker-mods/tree/jellyfin-opencl-intel.
            ;; This is just the prerequisite for hardware acceleration
            ;; support; see
            ;; https://github.com/linuxserver/docker-jellyfin?tab=readme-ov-file#hardware-acceleration-enhancements
            ;; for the full instructions.
            ;;
            ;; NOTE 2026-01-10: I believe that in order for the
            ;; linuxserver mods to be installed, the container must be
            ;; run at least once manually, not by Shepherd?
            "DOCKER_MODS=linuxserver/mods:jellyfin-opencl-intel"))
         (network "jellyfin-network")
         (ports '("127.0.0.1:17200:8096"))
         (volumes
          '(("/home/krisbalintona/services/jellyfin/data" . "/config")
            ("/home/krisbalintona/services/jellyfin/cache" . "/cache")
            ("/home/krisbalintona/services/media/shows" . "/data/shows")
            ("/home/krisbalintona/services/media/movies" . "/data/movies")))
         ;; Pass the appropriate GPU device to Jellyfin, as instructed
         ;; here:
         ;; https://jellyfin.org/docs/general/post-install/transcoding/hardware-acceleration/intel#configure-on-linux-host,
         ;; for the sake of hardware acceleration. The device is
         ;; specific to Intel GPUs.
         (extra-arguments '("--device=/dev/dri/renderD128:/dev/dri/renderD128:rwm"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

My device's CPU is Intel. To enable hardware acceleration, which greatly boosts the performance of transcoding and decreases the load on the system, the maintainer of my specific Jellyfin image (=linuxserver/jellyfin=) has provided instructions [[https://github.com/linuxserver/docker-jellyfin?tab=readme-ov-file#hardware-acceleration-enhancements][on their README]]. It involves, among other things (see the container configuration), enabling the following kernel module:
#+begin_src scheme :noweb-ref system-kernel-arguments
  ;; Enable the kernel module corresponding to step 2 in
  ;; https://jellyfin.org/docs/general/post-install/transcoding/hardware-acceleration/intel/#configure-and-verify-lp-mode-on-linux
  "i915.enable_guc=3"
#+end_src
To check if hardware acceleration is working, play a piece of media that should cause hardware acceleration.Then check the container logs for lines like containing this: "MediaBrowser.MediaEncoding.Encoder.MediaEncoder:". Those lines should suggest that =ffmpeg= is utilizing your hardware decoder.

*** Bazarr: Get subtitles

Good resources:
+ Setup guide: [[https://wiki.bazarr.media/Getting-Started/Setup-Guide/][Setup Guide - Bazarr Wiki]]
+ For good scoring settings: [[https://trash-guides.info/Bazarr/Bazarr-suggested-scoring/][Suggested Scoring - TRaSH Guides]]
+ Usage stats, allows you to find good subtitle providers per language: [[https://wiki.bazarr.media/bazarr-stats/][Bazarr Usage Stats - Bazarr Wiki]]
+ Good subtitle synchronization with audio settings: [[https://www.reddit.com/r/bazarr/comments/17ud9nt/comment/mm39err/?utm_source=share&utm_medium=web3x&utm_name=web3xcss&utm_term=1&utm_content=share_button][This Reddit comment's suggestions]]
  - For mass re-search of subtitles across all shows and/or movies (bazarr only allows this per-show, per-movie, and per-release): [[https://github.com/ajmandourah/bazarr-sync][GitHub - ajmandourah/bazarr-sync: Bulk sync your subtitles via Bazarr.]]

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-bazarr
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "bazarr")
         (image "linuxserver/bazarr:latest")
         (environment
          '("TZ='America/Chicago'"
            "PUID=1000"
            "PGID=1000"))
         (network "gluetun-network")
         (ports '("127.0.0.1:9799:6767"))
         (volumes
          '(("/home/krisbalintona/services/jellyfin/data" . "/config")
            ("/home/krisbalintona/services/media/shows" . "/data/shows")
            ("/home/krisbalintona/services/media/movies" . "/data/movies")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

** Monitoring

*** Goaccess: Web log access analytics
:PROPERTIES:
:CUSTOM_ID: h:20251222T054114.716045
:END:

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-goaccess
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "goaccess-network")
        (internal? #t))))
     (containers
      (list
       (oci-container-configuration
         (provision "goaccess")
         (image
           (oci-image
             (repository "goaccess")
             (tag "1.9.3")
             (value (specifications->manifest '("coreutils"
                                                "goaccess")))
             (pack-options '(#:symlinks (("/bin" -> "bin"))))))
         (network "goaccess-network")
         (ports '("127.0.0.1:7890:7890"))
         (volumes
          `(("/home/krisbalintona/services/caddy/log" . "/var/log/caddy")
            ("goaccess_web" . "/var/www/goaccess")))
         ;; Command taken from here:
         ;; https://dev.to/emrancu/setup-goaccess-in-ubuntulinux-with-docker-and-real-cad-access-over-domainsub-domain-226n
         ;;
         ;; Goaccess uses websockets for real-time updates.  We can
         ;; confirm that the goaccess page we see is receiving real-time
         ;; updates from the green dot on the top left of the page
         ;; (beside the burger menu icon).
         ;;
         ;; Caddy directs requests to our nselected domain to the HTTPS
         ;; port (443) of goaccess's network (i.e., the container
         ;; network).  As such, Caddy expects the goaccess's websocket
         ;; to be at wss://DOMAIN:443/ws.
         ;; 
         ;; (And we don't have to worry about passing SSL information to
         ;; the goaccess invocation certificates with the "tls internal"
         ;; Caddy setting.)
         ;;
         ;; Caddy just listens to the websocket to know when to update
         ;; the files it serves, but the actual file it serves is at a
         ;; path accessible in its container.  Caddy then knows to just
         ;; serve these files via the "file_server" setting.
         (command '("goaccess"
                    "/var/log/caddy/copyparty-json.log"
                    "/var/log/caddy/vaultwarden-json.log"
                    "--log-format=CADDY"
                    "-o" "/var/www/goaccess/index.html"
                    "--real-time-html"
                    "--ws-url=wss://goaccess.home.kristofferbalintona.me:443/ws"
                    "--port=7890"
                    "--tz='America/Chicago'"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

*** Gatus: Services monitor

A lightweight, simpler alternative to Uptime Kuma.

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-gatus
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "gatus")
         (image "twinproduction/gatus:stable")
         (network "host")
         (volumes
          `(,(cons (string-append (dirname (current-filename)) "/files/gatus/config.yaml")
                   "/config/config.yaml")
            "/home/krisbalintona/services/gatus/data:/data"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

* Backups

** Restic: Dead-simple and reliable CLI-based backups

#+begin_src scheme :noweb-ref home-modules
  (gnu services backup)
  (gnu home services backup)
#+end_src

#+begin_src scheme :noweb-ref home-helpers
  (define* (restic-job/defaults
            #:key
            (restic (@ (abbe packages golang) restic)) ; More up-to-date Restic
            name
            (repository "/mnt/backup-hdd")
            (password-file
             ;; FIXME 2025-12-22: This is evaluated at Guix evaluation
             ;; time, not at runtime.  If sops-guix ever changes its
             ;; secret path, this could drift.  This is currently the
             ;; most idiomatic solution without service-level
             ;; integration with sops-guix.
             (string-append "/run/user/"
                            (number->string (getuid))
                            "/secrets/restic-backup-password"))
            files
            schedule
            (wait-for-termination? #t)
            (extra-flags '("--retry-lock" "15m"))
            (verbose? #t))
    (restic-backup-job
      (restic restic)
      (name name)
      (files files)
      (schedule schedule)
      (repository repository)
      (password-file password-file)
      (wait-for-termination? wait-for-termination?)
      (extra-flags extra-flags)
      (verbose? verbose?)))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "restic"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-restic-backup-service-type)
  (simple-service 'home-restic-vault
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-vault"
      #:schedule "0 7-22/3 * * *"
      #:files (list (string-append (getenv "HOME") "/vault")))))
  <<home-restic-backup-jobs>>
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("restic-backup-password"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

* Home

#+begin_src scheme :tangle sublation-home.scm
  (use-modules (guix gexp)
               (gnu packages)
               (gnu system shadow)        ; For user-group 
               (gnu home services)
               (gnu home services shepherd)
               <<home-modules>>)

  <<home-helpers>>

  (home-environment
    (packages
     (specifications->packages
      (list
       ;; Editors
       "vim"
       "neovim"
       "emacs"
       ;; Other packages
       <<home-packages>>)))
    
    (services
     (cons*
      <<home-services>>
      
      (simple-service 'krisb-symlink-git-config-files-service-type
          home-xdg-configuration-files-service-type
        `(("git/config"
           ,(local-file "files/git/config"))))
      
      (service home-files-service-type
        `((".guile" ,%default-dotguile)
          (".Xdefaults" ,%default-xdefaults)))

      (service home-xdg-configuration-files-service-type
        `(("gdb/gdbinit" ,%default-gdbinit)
          ("nano/nanorc" ,%default-nanorc)))

      %base-home-services)))
#+end_src

* System

#+begin_src scheme :tangle sublation.scm
  (use-modules (gnu)
               (gnu system accounts)
               (gnu packages shells)
               (gnu services shepherd)
               (gnu services desktop)
               (gnu services xorg)
               (nongnu packages linux)
               (nongnu system linux-initrd)
               <<system-modules>>)

  (operating-system
    ;; Use Linux kernel.  See also the (nonguix transformations) module
    ;; from the nonguix channel for several convenient wrappers (e.g.,
    ;; nonguix-transformation-guix, nonguix-transformation-linux,
    ;; nonguix-transformation-nvidia)
    (kernel linux)
    (firmware (list linux-firmware))
    (kernel-arguments
     (cons*
      <<system-kernel-arguments>>
      %default-kernel-arguments))
    (initrd microcode-initrd)

    (locale "en_US.utf8")
    (timezone "America/Chicago")
    (keyboard-layout (keyboard-layout "us"))
    (host-name "sublation")
    
    ;; The list of user accounts ('root' is implicit).
    (users (cons* (user-account
                    (name "krisbalintona")
                    (comment "Kristoffer Balintona")
                    (group "users")
                    (home-directory "/home/krisbalintona")
                    ;; REVIEW 2025-12-06: Fish shell through tramp has
                    ;; failed despite all my attempts
                    ;; (shell (file-append fish "/bin/fish"))
                    (supplementary-groups '("wheel" "netdev"
                                            "audio" "video"
                                            <<supplementary-groups>>)))
                  %base-user-accounts))

    (packages
     (cons*
      <<system-packages>>
      glibc-locales
      %base-packages))

    ;; Below is the list of system services.  To search for available
    ;; services, run 'guix system search KEYWORD' in a terminal.
    (services
     (cons*
      <<system-services>>
      (service network-manager-service-type)
      (service wpa-supplicant-service-type)
      (service ntp-service-type)
      (service elogind-service-type
        (elogind-configuration
          ;; Inhibit laptop sleeping and hibernation on lid close
          (handle-lid-switch 'ignore)
          (handle-lid-switch-docked 'ignore)
          (handle-lid-switch-external-power 'ignore)
          (lid-switch-ignore-inhibited? #t)))
      %base-services))
    
    (bootloader (bootloader-configuration
                  (bootloader grub-efi-bootloader)
                  (targets (list "/boot/efi"))
                  (keyboard-layout keyboard-layout)))

    (file-systems (cons* (file-system
                           (mount-point "/mnt/backup-hdd")
                           (flags '(no-atime no-diratime))
                           (device (uuid
                                    "7526f253-87d5-47d9-80f9-66c99c70bb8f"
                                    'ext4))
                           (type "ext4")
                           (create-mount-point? #t)
                           (mount-may-fail? #t))
                         (file-system
                           (mount-point "/")
                           (device (uuid
                                    "849da8e9-c001-4a8b-9948-d996c774fc09"
                                    'ext4))
                           (type "ext4"))
                         (file-system
                           (mount-point "/boot/efi")
                           (device (uuid "8D5D-5605"
                                         'fat32))
                           (type "vfat"))
                         %base-file-systems)))
#+end_src
