#+TITLE: Sublation
#+PROPERTY: header-args :noweb yes

#  LocalWords:  CLI

* Shells

** Bash

#+begin_src scheme :noweb-ref home-modules
  (gnu home services shells)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-bash-service-type
    (home-bash-configuration
      (bashrc
       (list
        ;; 2025-12-06: My default shell is bash because I haven't gotten
        ;; Tramp to work with Fish shell yet.  (I've deduced that the
        ;; prompt is not the problem.)  So, instead, I keep bash as my
        ;; shell and dispatch to fish if the current process wasn't
        ;; started by tramp
        (plain-file "to-fish.bash"
          "if [[ $- == *i* ]] && { [[ ! $TERM =~ dumb ]] || [[ $TERM =~ eat ]]; }; then
      exec fish
  fi")))))
#+end_src

** Fish

#+begin_src scheme :noweb-ref home-modules
  (gnu home services shells)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-fish-service-type
    (home-fish-configuration
      (config
       (list (plain-file "fish_greeting.fish" "set -g fish_greeting")))))
#+end_src

* Tooling

** Jujutsu: Distributed VCS

#+begin_src scheme :noweb-ref home-modules
  (abbe packages rust)
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "jujutsu"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'krisb-symlink-jj-config-files-service-type
        home-xdg-configuration-files-service-type
      `(("jj/config.toml"
         ,(local-file "files/jujutsu/config.toml"))))
#+end_src

** System monitoring

#+begin_src scheme :noweb-ref home-packages
  "btop"
#+end_src

See also [[#h:20251222T054114.716045][Goaccess: Web log access analytics]].

* Authentication

** GnuPG

#+begin_src scheme :noweb-ref home-modules
  (gnu packages gnupg)
  (gnu home services gnupg)
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "gnupg"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-gpg-agent-service-type)
#+end_src

** SOPS: Secrets management

#+begin_src scheme :noweb-ref home-modules
  (sops secrets)
  (sops home services sops)
#+end_src

#+begin_src scheme :noweb-ref home-helpers
  (define sops-sublation-secrets-file
    (string-append (dirname (current-filename)) "/files/sops/sublation.yaml"))

  ;; Get secrets as strings.  Taken from
  ;; https://github.com/fishinthecalculator/sops-guix/issues/2
  (use-modules ((ice-9 popen) #:select (open-input-pipe close-pipe))
               ((rnrs io ports) #:select (get-string-all))
               ((sops secrets) #:select (sanitize-sops-key)))

  (define* (get-sops-secret key #:key file (number? #f))
    (let* ((cmd (format #f "sops --decrypt --extract '~a' '~a'"
                        (sanitize-sops-key key)
                        file))
           (port (open-input-pipe cmd))
           (secret (get-string-all port)))
      (close-pipe port)
      (if number?
          (string->number secret)
          secret)))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "age"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-sops-secrets-service-type
    (home-sops-service-configuration
      (config (local-file "files/sops/sops.yaml" "sops.yaml"))
      (secrets
       (list
        <<home-sops-secrets>>))))
#+end_src

* Podman: Containers

#+begin_src scheme :noweb-ref system-modules
  (gnu services containers)
  (gnu services sysctl)
#+end_src

#+begin_src scheme :noweb-ref supplementary-groups
  "cgroup"
#+end_src

#+begin_src scheme :noweb-ref system-services
  ;; Rootless podman also needs 'iptables-service-type' specifically for
  ;; its (non-internal) networks; 'nftables-service-type' will not
  ;; suffice.  See (guix) Miscellaneous Services.  We may have both,
  ;; though, since the default ruleset for iptables is to accept
  ;; everything.
  (service iptables-service-type)
  (service rootless-podman-service-type
    (rootless-podman-configuration
      (subgids (list (subid-range (name "krisbalintona"))))
      (subuids (list (subid-range (name "krisbalintona"))))))
  ;; TODO 2025-12-22: Figure out an ergonomic solution to avoid this.
  ;; For rootless podman services (Caddy and Pihole)
  (simple-service 'sysctl-podman
      sysctl-service-type
    '(("net.ipv4.ip_unprivileged_port_start" . "53")))
#+end_src

Use Podman by default for ~home-oci-service-type~.
#+begin_src scheme :noweb-ref home-services
  (service home-oci-service-type
    (for-home
     (oci-configuration
      (runtime 'podman)                   ; Use podman instead of docker
      (verbose? #t))))
#+end_src

* OpenSSH

#+begin_src scheme :noweb-ref system-modules
  (gnu services ssh)
#+end_src

#+begin_src scheme :noweb-ref system-services
  (service openssh-service-type)
#+end_src

#+begin_src scheme :noweb-ref home-modules
  (gnu home services ssh)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-openssh-service-type
      (home-openssh-configuration
        (hosts
         (list
          (openssh-host (name "WindowsG14")
                        (host-name "192.168.4.138")
                        (user "krisbalintona")
                        (forward-x11? #t)
                        (forward-x11-trusted? #t))))))
#+end_src

#+begin_src scheme :noweb-ref system-fail2ban-jails
  (simple-service 'fail2ban-openssh
      fail2ban-service-type
    (list
     (fail2ban-jail-configuration
       (name "sshd")
       (enabled? #t)
       (max-retry 5)
       (find-time "10m")
       (ban-time "1h"))))
#+end_src

* Self-hosting

#+begin_src scheme :noweb-ref home-helpers
  (define services-dir
    (string-append (getenv "HOME") "/services"))
#+end_src

** Security

*** Principles and guidelines

Guiding rules:
1. Set up a firewall.
2. Use a reverse proxy.
3. Each service gets their own container.
4. Use rootless containers.
5. Instead of =--network host=, each container has its own separate container network, preferably an internal one.
6. If publishing ports, don't publish to all interfaces, only to specific interfaces. That is, do =127.0.0.1:80:80= instead of =80:80=. We prefer the former to the latter since the latter permits listening to the ports from outside the host. (This is especially dangerous on some distros that bypass the firewall when publishing ports on e.g. Docker: port 80 ends up visible to the outside.)
7. When possible, prefer Unix Domain Sockets (UDS) (on shared container volumes) over publishing ports. Avoids having to have a network and published port(s) for containers.
8. If possible, use a non-root user for containers (even rootless ones).

Helpful tips for self-hosting:
+ [[https://www.reddit.com/r/selfhosted/comments/1ag9q88/comment/koftjss/?context=3][This Reddit comment]][fn:1]

Informative articles on security principles:
+ [[https://www.cloudflare.com/learning/security/glossary/what-is-zero-trust/][Zero Trust security | What is a Zero Trust network?]]

[fn:1] See also [[https://www.reddit.com/r/selfhosted/comments/1cv2l3q/security_psa_for_anyone_using_docker_on_a/][this relevant Reddit post]].
   
*** Firewall: Nftables

#+begin_src scheme :noweb-ref system-modules
  (gnu packages linux)
  (gnu services networking)
  (ice-9 textual-ports)                   ; For 'get-string-all'
  (krisb packages networking)
#+end_src

#+begin_src scheme :noweb-ref system-services
  (service nftables-service-type
    (nftables-configuration
      (ruleset
       (computed-file
           "nftables-ruleset"
         #~(call-with-output-file #$output
             (lambda (port)
               (format port
                       #$(call-with-input-file
                             (string-append (dirname (current-filename))
                                            "/files/nftables/sublation.nft")
                           get-string-all)
                       ;; Contains all country definitions/alias, e.g.,
                       ;; $US resolves to a particular number ID
                       (string-append #$nftables-geoip "/etc/nftables/geoip-def-all.nft")
                       ;; Map from IPv4 to country
                       (string-append #$nftables-geoip "/etc/nftables/geoip-ipv4.nft")
                       ;; Map from IPv6 to country
                       (string-append #$nftables-geoip "/etc/nftables/geoip-ipv6.nft"))))))))
#+end_src

*** Fail2ban: Scan logs and ban malicious actors 

Doing something like ~sudo fail2ban-client status --all~ shows the ban activity for all fail2ban jails.

#+begin_src scheme :noweb-ref system-modules
  (gnu services security)
#+end_src

Several of my services integrate with ~fail2ban-service-type~.
#+begin_src scheme :noweb-ref system-services
  (service fail2ban-service-type)
  <<system-fail2ban-jails>>
#+end_src

** Networking

Some useful tooling:
#+begin_src scheme :noweb-ref home-packages
  "bind:utils"
#+end_src

*** DNS

**** Unbound: Privacy-centered DNS resolver

#+begin_src scheme :noweb-ref system-modules
  (gnu services dns)
#+end_src

#+begin_src scheme :noweb-ref system-services
  ;; I use Unbound with Pihole.  For an explanation of why (and a guide
  ;; on how to do so) I pair Unbound with the latter, see
  ;; https://docs.pi-hole.net/guides/dns/unbound/
  (service unbound-service-type
    (unbound-configuration
      (server
       (unbound-server
         ;; Let only my machine query Unbound (Pihole forwards DNS
         ;; queries to Unbound)
         (interface '("127.0.0.1" ; IPv4 local host
                      "::1"))     ; IPv6 local
         (tls-cert-bundle "/etc/ssl/certs/ca-certificates.crt")
         (hide-version #t)
         (hide-identity #t)
         ;; See
         ;; https://unbound.docs.nlnetlabs.nl/en/latest/manpages/unbound.conf.html
         ;; for a description of all options
         (extra-options
          '((port . 5335) ; Pihole forwards DNS queries to this port
            ;; These are already default, but I declare them explicitly
            ;; anyway
            (do-ip4 . "yes")
            (do-udp . "yes")
            (do-tcp . "yes")
            ;; The following can be set to "yes" if my WAN supports
            ;; IPv6.  I can check by going to test-ipv6.com
            (do-ip6 . "no")
            ;; All paths below are relative to CHROOT, if set.  Make
            ;; sure CHROOT has the permissions of the unbound process.
            ;; The default user of the process is "unbound" (see also
            ;; the USERNAME option).  To change permissions, do
            ;; something like:
            ;;
            ;;     sudo chown unbound:unbound CHROOT_PATH
            ;;
            ;; I do not CHROOT since unbound already runs as a user
            ;; without elevated privileges, and that's good enough for
            ;; me.
            (chroot . "")
            ;; Logging.  If LOGFILE is a path, then ensure its parent
            ;; directory is created and that LOGFILE or its directory is
            ;; owned by "unbound" (the default value of the USERNAME
            ;; option).  If LOGFILE is not set or is set to an empty
            ;; string, then log output to "sudo herd status unbound"
            ;; instead
            (verbosity . "1")  ; Default
            (logfile . "")
            (log-time-ascii . "yes")
            (log-destaddr . "yes")
            (log-queries . "yes")
            (log-servfail . "yes")
            ;; TODO 2025-12-16: Write a service to do this
            ;; automatically.  But first check if there truly is no
            ;; currently existing way to do it automatically.
            ;;
            ;; Use DNSSEC.
            ;;
            ;; NOTE 2025-12-12: On Guix Systems, the root key has to be
            ;; created manually, it seems.  Ensure the parent directory
            ;; of the file exists then run:
            ;;
            ;;     sudo unbound-anchor -a /PATH/TO/KEY/root.key
            ;;
            ;; Unbound also runs as the "unbound" user, so for extra
            ;; security you can change the permissions of the file, too:
            ;;
            ;;     sudo chown unbound:unbound /PATH/TO/KEY
            ;;
            ;; Or, the first command can be ran with "-u unbound", like
            ;; so:
            ;;
            ;;     sudo -u unbound unbound-anchor -a /PATH/TO/KEY/root.key
            ;;
            (auto-trust-anchor-file . "/var/lib/unbound/root.key")
            (harden-glue . "yes")
            (harden-dnssec-stripped . "yes")
            ;; Give minimal domain name information in queries to DNS
            ;; servers.  See also the 'qname-minimisation-strict'
            ;; setting.
            (qname-minimisation . "yes")
            ;; See
            ;; https://docs.pi-hole.net/guides/dns/unbound/#configure-unbound
            ;; for an explanation of these values
            (edns-buffer-size . "1232")
            (so-rcvbuf . "1m")
            (use-caps-for-id . "no")
            (num-threads . "1")
            (prefetch . "yes")))))
      (remote-control
       (unbound-remote
         (control-enable #t)
         ;; Use with:
         ;;
         ;;     sudo unbound-control -s CONTROL-INTERFACE status
         ;;
         (control-interface "/run/unbound.sock"))) ; Default value
      ;; We place the below in EXTRA-CONTENT because we either need to
      ;; unquote a value entirely in the config or quote portions of
      ;; them (which can be done if the cdr is a single Guile symbol).
      ;; But UNBOUND-SERVER always quotes values, so to deal with edge
      ;; cases we place certain settings in EXTRA-CONTENT.  (There can
      ;; be multiple "server:" blocks in the config, it seems.)
      (extra-content
       "server:
          access-control: 127.0.0.0/8 allow

          # Ensure privacy of local IP ranges.  Taken from
          # https://docs.pi-hole.net/guides/dns/unbound/#configure-unbound
          private-address: 192.168.0.0/16
          private-address: 169.254.0.0/16
          private-address: 172.16.0.0/12
          private-address: 10.0.0.0/8
          private-address: fd00::/8
          private-address: fe80::/10

          # Ensure no reverse queries to non-public IP ranges (RFC6303
          # 4.2).  Taken from
          # https://docs.pi-hole.net/guides/dns/unbound/#configure-unbound
          private-address: 192.0.2.0/24
          private-address: 198.51.100.0/24
          private-address: 203.0.113.0/24
          private-address: 255.255.255.255/32
          private-address: 2001:db8::/32")))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "unbound"
#+end_src

**** Pi-hole: Block domains

Also do something like ~podman exec -it pihole padd~ for an ncurses, (CLI) UI for Pihole. And logs can be viewed in the CLI via ~podman exec -it pihole pihole tail~ (or just regular ~tail -F~ on any log file).

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("pihole-webserver-password"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-pihole
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (let* ((pihole-password-filename "pihole-webserver-password")
              (pihole-password-sops-file
               (string-append "/run/user/" (number->string (getuid)) "/secrets"
                              "/" pihole-password-filename))
              (pihole-password-file
               (string-append "/run/secrets/" pihole-password-filename)))
         (oci-container-configuration
           (provision "pihole")
           (image "docker.io/pihole/pihole:latest")
           (environment
            `("TZ=America/Chicago"
              "FTLCONF_dns_port=53" ; Default port
              ;; Listen for queries on all interfaces and from all
              ;; origins
              "FTLCONF_dns_listeningMode=ALL"
              ;; Forward queries to Unbound DNS (whose port is 5335 on
              ;; the host; we're using the host network in this
              ;; container so its IP is 127.0.0.1)
              "FTLCONF_dns_upstreams=127.0.0.1#5335"
              ;; Pihole as NTP server for other devices?
              "FTLCONF_ntp_ipv4_active=false"
              "FTLCONF_ntp_ipv6_active=false"
              ;; Pihole as NTP server for host?
              "FTLCONF_ntp_sync_active=false"
              ;; Webserver settings
              "FTLCONF_webserver_port=8080"
              ,(cons "WEBPASSWORD_FILE" pihole-password-file)))
           (network "host")
           (volumes
            `(("/home/krisbalintona/services/pihole/data" . "/etc/pihole")
              ,(cons pihole-password-sops-file pihole-password-file)))
           (auto-start? #t)
           (respawn? #f)))))))
#+end_src

*** Web traffic

**** Caddy: Reverse proxy

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("netlify-access-token"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-caddy
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "caddy")
         (image
           (oci-image
             ;; OCI images locations follow a
             ;; [registry]/[repository]:[tag] format.  Since we are
             ;; local, we only need to specify a repository and
             ;; optionally a tag.  The REPOSITORY field below
             ;; corresponds to the [repository] of an OCI image
             ;; location; it can be whatever we want since this image is
             ;; created locally (in the Guix store)
             (repository "caddy-netlify")
             (tag "2.10.2")
             (value (specifications->manifest '("caddy-netlify")))
             (pack-options '(#:symlinks (("/bin" -> "bin"))))))
         ;; These environment variables are set in the Docker image
         ;; Caddy distributes (shown by e.g. "podman image inspect
         ;; docker.io/caddy:2.10.2").  My tests show that they need to
         ;; be set for some reason
         (environment '("CADDY_VERSION=v2.10.2"
                        "XDG_CONFIG_HOME=/config"
                        "XDG_DATA_HOME=/data"))
         ;; Use the host network, then for services expose to the host
         ;; only the required ports and have Caddy direct traffic to
         ;; those ports.  An added benefit to using the host network is
         ;; that it permits Caddy to log the real IP of clients, since
         ;; the NAT of the Podman bridge network is no longer an
         ;; intermediary
         (network "host")
         (volumes
          `(("/home/krisbalintona/services/caddy/data" . "/data") ; Path of XDG_DATA_HOME
            ("/home/krisbalintona/services/caddy/log" . "/data/log") ; Where Caddy prints logs
            (,(string-append (dirname (current-filename)) "/files/caddy/Caddyfile")
             . "/config/Caddyfile")
            ;; Netlify access token
            (,(string-append "/run/user/" (number->string (getuid)) "/secrets"
                             "/netlify-access-token")
             ;; Reference this with a file placeholder in my Caddyfile;
             ;; see
             ;; https://caddyserver.com/docs/conventions#placeholders
             . "/run/secrets/netlify-access-token")
            ;; Goaccess real-time web page
            ("goaccess_web" . "/var/www/goaccess")
            ,(cons (string-append (getenv "XDG_RUNTIME_DIR") "/copyparty")
                   "/run/copyparty")))
         (command '("caddy" "run" "--config" "/config/Caddyfile"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

#+begin_src scheme :noweb-ref system-fail2ban-jails
  (simple-service 'fail2ban-caddy
      fail2ban-service-type
    (list 
     (fail2ban-jail-configuration
       (name "caddy-bots")
       (enabled? #t)
       (max-retry 3)
       (find-time "5m")
       (ban-time "1h")
       (log-path
        '("/home/krisbalintona/services/caddy/log/copyparty.log"))
       (filter
        (fail2ban-jail-filter-configuration
          (name "nginx-botsearch"))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-caddy
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-caddy"
      #:schedule "0 0 */2 * *"
      #:files (list (string-append services-dir "/caddy")))))
#+end_src

**** Goaccess: Web log access analytics
:PROPERTIES:
:CUSTOM_ID: h:20251222T054114.716045
:END:

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-goaccess
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "goaccess-network")
        (internal? #t))))
     (containers
      (list
       (oci-container-configuration
         (provision "goaccess")
         (image
           (oci-image
             (repository "goaccess")
             (tag "1.9.3")
             (value (specifications->manifest '("goaccess")))
             (pack-options '(#:symlinks (("/bin" -> "bin"))))))
         (network "goaccess-network")
         (ports '("127.0.0.1:7890:7890"))
         (volumes
          `(("/home/krisbalintona/services/caddy/log" . "/var/log")
            ("goaccess_web" . "/var/www/goaccess")))
         ;; Command taken from here:
         ;; https://dev.to/emrancu/setup-goaccess-in-ubuntulinux-with-docker-and-real-cad-access-over-domainsub-domain-226n
         ;;
         ;; Goaccess uses websockets for real-time updates.  We can
         ;; confirm that the goaccess page we see is receiving real-time
         ;; updates from the green dot on the top left of the page
         ;; (beside the burger menu icon).
         ;;
         ;; Caddy directs requests to our nselected domain to the HTTPS
         ;; port (443) of goaccess's network (i.e., the container
         ;; network).  As such, Caddy expects the goaccess's websocket
         ;; to be at wss://DOMAIN:443/ws.
         ;; 
         ;; (And we don't have to worry about passing SSL information to
         ;; the goaccess invocation certificates with the "tls internal"
         ;; Caddy setting.)
         ;;
         ;; Caddy just listens to the websocket to know when to update
         ;; the files it serves, but the actual file it serves is at a
         ;; path accessible in its container.  Caddy then knows to just
         ;; serve these files via the "file_server" setting.
         (command '("goaccess"
                    "/var/log/copyparty-json.log"
                    "--log-format=CADDY"
                    "-o" "/var/www/goaccess/index.html"
                    "--real-time-html"
                    "--ws-url=wss://goaccess.home.arpa:443/ws"
                    "--port=7890"
                    "--tz='America/Chicago'"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

**** Wireguard: VPN

#+begin_src scheme :noweb-ref system-modules
  (gnu services vpn)
  (gnu services sysctl)
#+end_src

#+begin_src scheme :noweb-ref system-services
  (service wireguard-service-type
    (wireguard-configuration
      (shepherd-requirement '(nftables))
      (addresses '("10.0.0.1/24"))
      ;; TODO 2025-12-21: Avoid hardcoding this
      (private-key "/run/user/1000/secrets/wireguard-private-key")
      (bootstrap-private-key? #f)
      ;; Network rules
      (pre-up
       (list
        ;; IPv4 and IPv6 table for address translation (rewriting packet
        ;; addresses)
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " add table inet wg-nat")
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " add chain inet wg-nat postrouting '{ type nat hook postrouting priority -100; }'")
        ;; Rewrite source IP of Wireguard outgoing packets (from
        ;; OnePlus) leaving via wifi to be the host's IP
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " add rule inet wg-nat postrouting oifname 'wlp108s0'"
                         " ip saddr 10.0.0.0/24 masquerade")))
      (post-down
       (list
        #~(string-append #$(file-append nftables "/sbin/nft")
                         " delete table inet wg-nat")))
      (peers
       (list
        (wireguard-peer
          (name "OnePlus")
          (public-key "Mgj/EOTOJv96q6NSN8CC7DEXB7ic6WV78H1ukHm7fyY=")
          (allowed-ips '("10.0.0.2/32")))))))
  ;; For Wireguard IP forwarding
  (simple-service 'sysctl-wireguard
      sysctl-service-type
    '(("net.ipv4.ip_forward" . "1")))
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("wireguard-private-key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

** Services

*** Copyparty

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services shepherd)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-copyparty-socket
      home-shepherd-service-type
    (list
     (shepherd-service
       (provision '(home-oci-copyparty-socket))
       (one-shot? #t)
       (start
        #~(lambda ()
            (let ((dir (string-append (getenv "XDG_RUNTIME_DIR") "/copyparty")))
              (mkdir-p dir)
              (format #t "Socket directory exists at: ~a~%" dir)
              #t)))
       (documentation "Create parent directory for Copyparty socket."))))
  (simple-service 'home-oci-copyparty
      home-oci-service-type
    (oci-extension
     (containers
      (list
       (oci-container-configuration
         (provision "copyparty")
         (requirement '(home-oci-copyparty-socket))
         (image "docker.io/copyparty/ac:1.19.21")
         ;; Have files mounted at /data and copyparty config + cache
         ;; files in /srv
         (volumes
          `(,(cons (string-append (getenv "XDG_RUNTIME_DIR") "/copyparty")
                   "/run/copyparty")
            ("/home/krisbalintona/services/copyparty/data" . "/data")
            (,(string-append (dirname (current-filename)) "/files/copyparty/copyparty.conf")
             . "/srv/copyparty.conf")))
         (command '("-c" "/srv/copyparty.conf" "--chdir" "/srv"))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

#+begin_src scheme :noweb-ref home-restic-backup-jobs
  (simple-service 'home-restic-copyparty
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-copyparty"
      #:schedule "0 12 * * *"
      #:files (list (string-append services-dir "/copyparty")))))
#+end_src

*** Vaultwarden

#+begin_src scheme :noweb-ref home-modules
  (gnu services containers)
  (gnu home services containers)
#+end_src

#+begin_src scheme :noweb-ref home-services
  (simple-service 'home-oci-vaultwarden
      home-oci-service-type
    (oci-extension
     (networks
      (list
       (oci-network-configuration
        (name "vaultwarden-network"))))
     (containers
      (list
       (oci-container-configuration
         (provision "vaultwarden")
         (image "vaultwarden/server:latest")
         (environment
          `("ADMIN_TOKEN='$argon2id$v=19$m=65540,t=3,p=4$T4YVDSINU+4alWZ22logYyqgUbQn4J4o2DAW/deZF3o$zKnbtTREy8wxrCDEAne1F58/CXBHRiKkII9NqsoGVJA'"
            "DOMAIN=https://vault.kristofferbalintona.me"
            ;; Logging
            "LOG_FILE=/var/log/vaultwarden/vaultwarden.log"
            "LOG_LEVEL=debug"
            "EXTENDED_LOGGING=true"
            ;; Push notification support.  Also requires (i) the
            ;; "firebaseinstallations.googleapis.com" domain not to be
            ;; blocked and (ii) be able to make outbound HTTPS
            ;; connections (e.g., requires an "external" container
            ;; network).  See
            ;; https://github.com/dani-garcia/vaultwarden/wiki/Enabling-Mobile-Client-push-notification
            "PUSH_ENABLED=true"
            ,(cons "PUSH_INSTALLATION_ID"
                   (get-sops-secret '("vaultwarden" "push-installation-id")
                                    #:file sops-sublation-secrets-file))
            ,(cons "PUSH_INSTALLATION_KEY"
                   (get-sops-secret '("vaultwarden" "push-installation-key")
                                    #:file sops-sublation-secrets-file))
            ;; Settings relevant for security.  See also
            ;; https://github.com/dani-garcia/vaultwarden/wiki/Hardening-Guide
            "SHOW_PASSWORD_HINT=false"
            "SIGNUPS_ALLOWED=false"))
         (network "vaultwarden-network")
         (ports '("127.0.0.1:7000:80"))
         (volumes '(("/home/krisbalintona/services/vaultwarden/data" . "/data")))
         (auto-start? #t)
         (respawn? #f))))))
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("vaultwarden" "push-installation-id"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
  (sops-secret
    (key '("vaultwarden" "push-installation-key"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

* Backups

** Restic

#+begin_src scheme :noweb-ref home-modules
  (gnu services backup)
  (gnu home services backup)
#+end_src

#+begin_src scheme :noweb-ref home-helpers
  (define* (restic-job/defaults
            #:key
            name
            (repository "/mnt/backup-hdd")
            (password-file
             ;; FIXME 2025-12-22: This is evaluated at Guix evaluation
             ;; time, not at runtime.  If sops-guix ever changes its
             ;; secret path, this could drift.  This is currently the
             ;; most idiomatic solution without service-level
             ;; integration with sops-guix.
             (string-append "/run/user/"
                            (number->string (getuid))
                            "/secrets/restic-backup-password"))
            files
            schedule
            (extra-flags '())
            (verbose? #t))
    (restic-backup-job
      (name name)
      (files files)
      (schedule schedule)
      (repository repository)
      (password-file password-file)
      (extra-flags extra-flags)
      (verbose? verbose?)))
#+end_src

#+begin_src scheme :noweb-ref home-packages
  "restic"
#+end_src

#+begin_src scheme :noweb-ref home-services
  (service home-restic-backup-service-type)
  (simple-service 'home-restic-vault
      home-restic-backup-service-type
    (list
     (restic-job/defaults
      #:name "restic-vault"
      #:schedule "0 7-22/3 * * *"
      #:files (list (string-append (getenv "HOME") "/vault")))))
  <<home-restic-backup-jobs>>
#+end_src

#+begin_src scheme :noweb-ref home-sops-secrets
  (sops-secret
    (key '("restic-backup-password"))
    (file (local-file "files/sops/sublation.yaml"))
    (permissions #o400))
#+end_src

* Home

#+begin_src scheme :tangle sublation-home.scm
  (use-modules (guix gexp)
               (gnu packages)
               (gnu system shadow)        ; For user-group 
               (gnu home services)
               (gnu home services shepherd)
               <<home-modules>>)

  <<home-helpers>>

  (home-environment
    (packages
     (specifications->packages
      (list
       "glibc-locales"
       "git"
       "make"
       "vim"
       "neovim"
       "tree"
       "ripgrep"
       "parted"
       "emacs"
       "brightnessctl"
       <<home-packages>>)))
    
    (services
     (cons*
      <<home-services>>
      
      (simple-service 'guix-locales
          home-environment-variables-service-type
        '(("GUIX_LOCPATH" . "$HOME/.guix-profile/lib/locale")))
      (simple-service 'krisb-symlink-git-config-files-service-type
          home-xdg-configuration-files-service-type
        `(("git/config"
           ,(local-file "files/git/config"))))
      
      (service home-files-service-type
        `((".guile" ,%default-dotguile)
          (".Xdefaults" ,%default-xdefaults)))

      (service home-xdg-configuration-files-service-type
        `(("gdb/gdbinit" ,%default-gdbinit)
          ("nano/nanorc" ,%default-nanorc)))

      %base-home-services)))
#+end_src

* System

#+begin_src scheme :tangle sublation.scm
  (use-modules (gnu)
               (gnu system accounts)
               (gnu packages shells)
               (gnu services shepherd)
               (gnu services desktop)
               (gnu services xorg)
               (nonguix transformations)
               <<system-modules>>)

  ((compose (nonguix-transformation-guix)
            (nonguix-transformation-linux))
   (operating-system
     (locale "en_US.utf8")
     (timezone "America/Chicago")
     (keyboard-layout (keyboard-layout "us"))
     (host-name "sublation")

     ;; The list of user accounts ('root' is implicit).
     (users (cons* (user-account
                     (name "krisbalintona")
                     (comment "Kristoffer Balintona")
                     (group "users")
                     (home-directory "/home/krisbalintona")
                     ;; REVIEW 2025-12-06: Fish shell through tramp has
                     ;; failed despite all my attempts
                     ;; (shell (file-append fish "/bin/fish"))
                     (supplementary-groups '("wheel" "netdev"
                                             "audio" "video"
                                             <<supplementary-groups>>)))
                   %base-user-accounts))

     (packages
      (cons*
       <<system-packages>>
       glibc-locales
       %base-packages))

     ;; Below is the list of system services.  To search for available
     ;; services, run 'guix system search KEYWORD' in a terminal.
     (services
      (cons*
       <<system-services>>
       (service network-manager-service-type)
       (service wpa-supplicant-service-type)
       (service ntp-service-type)
       (service elogind-service-type
         (elogind-configuration
           ;; Inhibit laptop sleeping and hibernation on lid close
           (handle-lid-switch 'ignore)
           (handle-lid-switch-docked 'ignore)
           (handle-lid-switch-external-power 'ignore)
           (lid-switch-ignore-inhibited? #t)))
       %base-services))
     
     (bootloader (bootloader-configuration
                   (bootloader grub-efi-bootloader)
                   (targets (list "/boot/efi"))
                   (keyboard-layout keyboard-layout)))

     (file-systems (cons* (file-system
                            (mount-point "/mnt/backup-hdd")
                            (flags '(no-atime no-diratime))
                            (device (uuid
                                     "7526f253-87d5-47d9-80f9-66c99c70bb8f"
                                     'ext4))
                            (type "ext4")
                            (create-mount-point? #t)
                            (mount-may-fail? #t))
                          (file-system
                            (mount-point "/")
                            (device (uuid
                                     "849da8e9-c001-4a8b-9948-d996c774fc09"
                                     'ext4))
                            (type "ext4"))
                          (file-system
                            (mount-point "/boot/efi")
                            (device (uuid "8D5D-5605"
                                          'fat32))
                            (type "vfat"))
                          %base-file-systems))))
#+end_src
